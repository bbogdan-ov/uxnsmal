// ====================
// MOUSE CURSOR example.
// ====================

// Devices layout.
untyped enum byte System {
	r { 0x08 }
	g { 0x0a }
	b { 0x0c }
}

untyped enum byte Screen {
	vector { 0x20 }
	width  { 0x22 }
	height { 0x24 }
	auto   { 0x26 }
	x      { 0x28 }
	y      { 0x2a }
	addr   { 0x2c }
	pixel  { 0x2e }
	sprite { 0x2f }
}

untyped enum byte Mouse {
	vector { 0x90 }
	x      { 0x92 }
	y      { 0x94 }
	state  { 0x96 }
}

// Constants.
const short SIZE { 128* }

// Variables (always 0 initialized).
var short x
var short y

// When game started...
fun on-reset ( -> ) {
	// Setup color palette.
	0xf07f* System.r output
	0xf0d6* System.g output
	0xf0b2* System.b output

	// Setup window size.
	SIZE Screen.width output
	SIZE Screen.height output

	// "Subscribe" to mouse events.
	&on-mouse Mouse.vector output
}

// On any mouse event...
fun on-mouse ( -> ) {
	// Clear previous cursor sprite.
	x Screen.x output
	y Screen.y output
	&sprite-cursor Screen.addr output
	0b1000000 Screen.sprite output

	// Store new mouse position.
	Mouse.y input2 &y store
	Mouse.x input2 &x store

	// Draw cursor sprite in the new mouse position.
	x Screen.x output
	y Screen.y output
	&sprite-cursor Screen.addr output

	// Change cursor color based on whether any mouse button is pressed.
	Mouse.state input if {
		0b1000011 // pressed
	} else {
		0b1000010 // not pressed
	}

	Screen.sprite output
}

// Cursor sprite.
data sprite-cursor {
	0b10000000
	0b11000000
	0b11100000
	0b11110000
	0b11111000
	0b11100000
	0b00010000
	0b00000000
}
