// ==========================================
// LOGO and it bounces of the screen edges!!!
// ==========================================

const byte System.r { 0x08 }
const byte System.g { 0x0a }
const byte System.b { 0x0c }

const byte Screen.vector { 0x20 }
const byte Screen.width  { 0x22 }
const byte Screen.height { 0x24 }
const byte Screen.auto   { 0x26 }
const byte Screen.x      { 0x28 }
const byte Screen.y      { 0x2a }
const byte Screen.addr   { 0x2c }
const byte Screen.pixel  { 0x2e }
const byte Screen.sprite { 0x2f }

const short SCREEN_WIDTH  { 256* }
const short SCREEN_HEIGHT { 200* }

const short LOGO_WIDTH     { 0x04* }
const short LOGO_HEIGHT    { 0x03* }
const short LOGO_WIDTH_PX  { LOGO_WIDTH 8* mul }
const short LOGO_HEIGHT_PX { LOGO_HEIGHT 8* mul }
const short LOGO_LEN       { LOGO_WIDTH LOGO_HEIGHT mul }
const short LOGO_SPEED     { 2* }

var short logo.x
var short logo.y
var byte  logo.dir.x
var byte  logo.dir.y

fun on-reset ( -> ) {
	// Setup color palette
	0x0ff0* System.r output
	0x0f0f* System.g output
	0x0f00* System.b output

	// Setup window size
	SCREEN_WIDTH Screen.width output
	SCREEN_HEIGHT Screen.height output

	// Setup logo
	130* &logo.x store
	66*  &logo.y store
	128  &logo.dir.x store
	128  &logo.dir.y store

	&on-screen Screen.vector output
}

fun on-screen ( -> ) {
	// Clear previous logo sprite
	0b00000000 logo.x logo.y logo.draw

	logo.update

	// Draw logo
	0b00000001 logo.x logo.y logo.draw
}

fun logo.update ( -- ) {
	logo.dir.x 128 gth if {
		// Move right
		logo.x LOGO_SPEED add &logo.x store
	} else {
		// Move left
		logo.x LOGO_SPEED sub &logo.x store
	}

	logo.dir.y 128 gth if {
		// Move down
		logo.y LOGO_SPEED add &logo.y store
	} else {
		// Move up
		logo.y LOGO_SPEED sub &logo.y store
	}

	// Bounce of the left wall
	logo.dir.x
	logo.x 1* lth
	add &logo.dir.x store

	// Bounce of the right wall
	logo.dir.x
	logo.x LOGO_WIDTH_PX add SCREEN_WIDTH gth
	sub &logo.dir.x store

	// Bounce of the top wall
	logo.dir.y
	logo.y 1* lth
	add &logo.dir.y store

	// Bounce of the bottom wall
	logo.dir.y
	logo.y LOGO_HEIGHT_PX add SCREEN_HEIGHT gth
	sub &logo.dir.y store
}

fun logo.draw ( byte short short -- ) {
	Screen.y output
	Screen.x output

	// Draw logo
	0b00110110 Screen.auto output
	&sprite.logo Screen.addr output
	Screen.sprite output-k output-k output
}

data sprite.logo {
	0x00 0x00 0x00 0x38 0x38 0x78 0x78 0xf0 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00
	0x07 0x07 0x07 0x01 0x00 0x00 0x00 0x00 0x80 0xc0 0xe0 0xf0 0xf0 0x78 0x78 0x38
	0xf0 0xe0 0xe0 0xe3 0xe3 0xe3 0xe0 0xe0 0x00 0x00 0x79 0xf9 0xf9 0xc0 0x00 0x00
	0x00 0x3e 0xfe 0xfe 0xe0 0x00 0x00 0x00 0x38 0x38 0x38 0x38 0x38 0x38 0x38 0x78
	0xe0 0xf0 0xfc 0x7c 0x3c 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00
	0x00 0x01 0x01 0x01 0x00 0x00 0x00 0x00 0xf8 0xf0 0xe0 0xc0 0x00 0x00 0x00 0x00
}
